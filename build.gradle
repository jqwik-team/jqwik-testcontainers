plugins {
	id 'java-library'
	id 'maven-publish'
	id 'signing'
}

static isSnapshotRelease(versionString) {
	versionString.endsWith('SNAPSHOT')
}

ext {
	moduleName = 'net.jqwik.testcontainers'
	junitPlatformVersion = '1.8.1'
	opentest4jVersion = '1.2.0'
	assertJVersion = '3.15.0'
	mockitoVersion = '3.3.3'
	jqwikVersion = '1.5.6'
	testcontainersVersion = '1.16.2'
	jqwikTestcontainersVersion = '0.5.1-SNAPSHOT'
	hikariCpVersion =  '3.4.5'
	jedisClientVersion = '3.3.0'
	httpClientVersion = '4.5.12'
	postgreSqlClientVersion = '42.2.16'
	mySqlClientVersion = '8.0.21'
	isSnapshotRelease = isSnapshotRelease(jqwikTestcontainersVersion)
}

repositories {
	mavenCentral()
	maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

group = moduleName
version = jqwikTestcontainersVersion

jar {
	archiveBaseName.set('jqwik-testcontainers')
	archiveVersion.set("${jqwikTestcontainersVersion}")
	manifest {
		attributes('Automatic-Module-Name': "${moduleName}")
	}
}

java {
	withJavadocJar()
	withSourcesJar()
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
}

compileTestJava {
	options.compilerArgs += '-parameters'
	options.encoding = 'UTF-8'
}

test {
	useJUnitPlatform {
		includeEngines 'jqwik'
	}

	include '**/*Properties.class'
	include '**/*Example.class'
	include '**/*Examples.class'
	include '**/*Test.class'
	include '**/*Tests.class'

	// testLogging.showStandardStreams = true
}


dependencies {
	api("org.opentest4j:opentest4j:${opentest4jVersion}")
	api("net.jqwik:jqwik:${jqwikVersion}")
	compileOnly("org.testcontainers:testcontainers:${testcontainersVersion}")

	testImplementation("org.testcontainers:mysql:${testcontainersVersion}")
	testImplementation("org.testcontainers:postgresql:${testcontainersVersion}")
	testImplementation("com.zaxxer:HikariCP:${hikariCpVersion}")
	testImplementation("redis.clients:jedis:${jedisClientVersion}")
	testImplementation("org.apache.httpcomponents:httpclient:${httpClientVersion}")
	testImplementation("org.assertj:assertj-core:${assertJVersion}")
	testImplementation("org.mockito:mockito-core:${mockitoVersion}")
	testImplementation("org.mockito:mockito-junit-jupiter:${mockitoVersion}")
	testRuntimeOnly("org.postgresql:postgresql:${postgreSqlClientVersion}")
	testRuntimeOnly("mysql:mysql-connector-java:${mySqlClientVersion}")
}

publishing {
	repositories {
		maven {
			// hint: password is in ~/.gradle/gradle.properties
			def ossrhUsername = project.hasProperty('ossrhUsername') ? project.ossrhUsername : ''
			def ossrhPassword = project.hasProperty('ossrhPassword') ? project.ossrhPassword : ''

			credentials {
				username = ossrhUsername
				password = ossrhPassword
			}

			// change URLs to point to your repos, e.g. http://my.org/repo
			def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
			def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
			url = isSnapshotRelease ? snapshotsRepoUrl : releasesRepoUrl
		}
	}
	publications {
		jqwikTestcontainers(MavenPublication) {
			groupId = 'net.jqwik'
			artifactId = 'jqwik-testcontainers'
			from components.java
			pom {
				groupId = 'net.jqwik'
				name = 'jqwik-testcontainers'
				description = "Jqwik Testcontainers support module"
				url = 'https://github.org/jlink/jqwik-testcontainers'
				licenses {
					license {
						name = 'Eclipse Public License - v 2.0'
						url = 'http://www.eclipse.org/legal/epl-v20.html'
					}
				}
				developers {
					developer {
						id = 'gtrefs'
						name = 'Gregor Trefs'
						email = 'Gregor.Trefs@gmail.com'
					}
					developer {
						id = 'jlink'
						name = 'Johannes Link'
						email = 'business@johanneslink.net'
					}
				}
				scm {
					connection = 'scm:git:git://github.com/jlink/jqwik-testcontainers.git'
					developerConnection = 'scm:git:git://github.com/jlink/jqwik-testcontainers.git'
					url = 'https://github.com/jlink/jqwik-testcontainers'
				}
			}
		}
	}
}

signing {
	if (!isSnapshotRelease) {
		sign publishing.publications.jqwikTestcontainers
	}
}

wrapper {
	gradleVersion = '7.2'
}
